Next    = - d:Datum                      { ob = d; }
        | - e:EOF                        { ob = e; }
        | j:Junk                         { ob = j; }

Datum   = Quote | Const | List 
        | Number | Symbol | String

LP      = '(' -
RP      = ')' -
Dot     = '.' Space -

List     = LP Tail
Tail     = a:Datum Dot d:Datum RP        { $$= CONS(a,d) }
         | RP                            { $$= NIL }
         | d:Datum t:Tail                { $$= CONS(d,t) }

Quote   = ['] - d:Datum                  { $$= QUOTE(d) }

Const   = True | False

True    = '#t' -                         { $$= TRUE }
False   = '#f' -                         { $$= FALSE }

Junk    = < .* > EOF                     { $$= JUNK(yytext) }

Number	= < Digit+ > !NonDigit -	 { $$= NUMBER(atoi(yytext)) }
Symbol  = < Char+ > -	                 { $$= SYMBOL(yytext) }
String  = ["] < ( !["] Char )* > ["] -   { $$= STRING(yytext) }


Letter   = [A-Za-z]
Digit    = [0-9]
Special  = '!' | '-' | '?' | '*' 
         | '_' | ':' | '<' | '>' 
         | '=' | '/' | '+'
NonDigit = Letter | Special
Char     = Digit | NonDigit

Space   = [ \t\n]
-	= ( Space | Comment )*

Comment = ';' ( !EOL . )* EOL
EOL	= '\n' | '\r\n' | '\r'

EOF     = !.                             { $$= EOF_OBJECT }



