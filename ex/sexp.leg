Read    = - n:Next                       { ob = n; }

Next    = Datum | EOF | Junk

Datum   = Quote | UQuote | QQuote
        | Const | List | SList # | Qlist
        | NNumber | Number | Symbol | String

List    = LP Tail
Tail    = a:Datum Dot d:Datum RP         { $$= CONS(a,d) }
        | RP                             { $$= NIL }
        | d:Datum t:Tail                 { $$= CONS(d,t) }

SList   = SLP t:STail                    { $$= SQUARE(t) }
STail   = a:Datum Dot d:Datum SRP        { $$= CONS(a,d) }
        | SRP                            { $$= NIL }
        | d:Datum t:STail                { $$= CONS(d,t) }

## This doesn't nest properly
# QList   = QLP t:QTail                    { $$= QUOTE(t) }
# QTail   = a:Datum Dot d:Datum QRP        { $$= CONS(a,d) }
#         | QRP                            { $$= NIL }
#         | d:Datum t:QTail                { $$= CONS(d,t) }


Quote   = - ['] - d:Datum                { $$= QUOTE(d) }
UQuote  = - [,] - d:Datum                { $$= UNQUOTE(d) }
QQuote  = - [`] - d:Datum                { $$= QUASIQUOTE(d) }

Const   = True | False

LP      = - '('
RP      = - ')'
# QLP     = - '{'
# QRP     = - '}'
SLP     = - '['
SRP     = - ']'
Dot     = Space - '.' Space
True    = - '#t'                         { $$= TRUE }
False   = - '#f'                         { $$= FALSE }

NNumber = - < '-' Digit+ > !NonDigit     { $$= NUMBER(atoi(yytext)) }
Number	= - < Digit+ > !NonDigit         { $$= NUMBER(atoi(yytext)) }
Symbol  = - < Char+ >  	                 { $$= SYMBOL(yytext) }
String  = - ["] < ( !["] . )* > ["]      { $$= STRING(yytext) }


Letter   = [A-Za-z]
Digit    = [0-9]
Reserved = [()"'`,;\[\]]
Special  = !(Reserved | Space | Letter | Digit) .
NonDigit = Letter | Special
Char     = Digit | NonDigit

Space   = [ \t\n\r]
-	= ( Space | Comment )*

Comment = ';' ( !EOL . )* EOL
EOL	= '\n' | '\r\n' | '\r'

EOF     = !.                             { $$= EOF_OBJECT }

Junk    = < .* > EOF                     { $$= JUNK(yytext) }




